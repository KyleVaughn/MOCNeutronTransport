export AABox
export measure, maxima, minima, xmin, ymin, zmin, xmax, ymax, zmax, Œîx, Œîy, Œîz,
       facets, ridges, peaks, faces, edges, vertices

"""
    AABox(minima::Point{D,T}, maxima::Point{D,T})

A `D`-dimensional axis-aligned bounding box using two `D`-dimensional
points, representing the `minima` and `maxima` of the box. 
"""
struct AABox{D, T}
    minima::Point{D, T}
    maxima::Point{D, T}
end

# constructors
AABox(minima, maxima) = AABox(Point(minima), Point(maxima))

maxima(aab::AABox) = aab.maxima
minima(aab::AABox) = aab.minima
function Base.isapprox(aab‚ÇÅ::AABox, aab‚ÇÇ::AABox)
    return minima(aab‚ÇÅ) ‚âà minima(aab‚ÇÇ) &&
           maxima(aab‚ÇÅ) ‚âà maxima(aab‚ÇÇ)
end
function Base.union(aab‚ÇÅ::AABox, aab‚ÇÇ::AABox)
    return AABox(min.(coordinates(minima(aab‚ÇÅ)), coordinates(minima(aab‚ÇÇ))),
                 max.(coordinates(maxima(aab‚ÇÅ)), coordinates(maxima(aab‚ÇÇ))))
end

xmin(aab::AABox) = aab.minima[1]
ymin(aab::AABox) = aab.minima[2]
zmin(aab::AABox{3}) = aab.minima[3]
xmax(aab::AABox) = aab.maxima[1]
ymax(aab::AABox) = aab.maxima[2]
zmax(aab::AABox{3}) = aab.maxima[3]
Œîx(aab::AABox) = xmax(aab) - xmin(aab)
Œîy(aab::AABox) = ymax(aab) - ymin(aab)
Œîz(aab::AABox{3}) = zmax(aab) - zmin(aab)
measure(aab::AABox) = prod(maxima(aab) - minima(aab))
ridges(aab::AABox{2}) = vertices(aab)
facets(aab::AABox{2}) = edges(aab)
peaks(aab::AABox{3}) = vertices(aab)
ridges(aab::AABox{3}) = edges(aab)
facets(aab::AABox{3}) = faces(aab)

@inline function Base.in(p::Point{2}, aab::AABox{2})    
    return xmin(aab) ‚â§ p[1] ‚â§ xmax(aab) &&    
           ymin(aab) ‚â§ p[2] ‚â§ ymax(aab)    
end  

function vertices(aab::AABox{2})
    # Ordered CCW
    return Vec(Point(xmin(aab), ymin(aab)),
               Point(xmax(aab), ymin(aab)),
               Point(xmax(aab), ymax(aab)),
               Point(xmin(aab), ymax(aab)))
end

function edges(aab::AABox{2})
    v = vertices(aab)
    return Vec(LineSegment(v[1], v[2]),
               LineSegment(v[2], v[3]),
               LineSegment(v[3], v[4]),
               LineSegment(v[4], v[1]))
end

function vertices(aab::AABox{3})
    # in CCW order, low z then high z
    #      y
    #      ^
    #      |
    #      |
    #      |------> x
    #     /   
    #    /   
    #   ùò∑
    #  z
    #
    #   4----3
    #  /    /|
    # 8----7 |
    # |    | 2
    # |    |/
    # 5----6
    return Vec(Point(xmin(aab), ymin(aab), zmin(aab)),
               Point(xmax(aab), ymin(aab), zmin(aab)),
               Point(xmax(aab), ymax(aab), zmin(aab)),
               Point(xmin(aab), ymax(aab), zmin(aab)),
               Point(xmin(aab), ymin(aab), zmax(aab)),
               Point(xmax(aab), ymin(aab), zmax(aab)),
               Point(xmax(aab), ymax(aab), zmax(aab)),
               Point(xmin(aab), ymax(aab), zmax(aab)))
end

function edges(aab::AABox{3})
    # in CCW order, low z, then high z, then the segments that attach low and
    # high z in CCW order.
    #      y
    #      ^
    #      |
    #      |
    #      |------> x
    #     /   
    #    /   
    #   ùò∑
    #  z
    #       3
    #    +----+
    # 11/ 7  /|
    #  +----+ | 2
    # 8|    |6+
    #  |    |/ 9
    #  +----+
    #     5
    v = vertices(aab)
    return Vec(LineSegment(v[1], v[2]), # lower z
               LineSegment(v[2], v[3]),
               LineSegment(v[3], v[4]),
               LineSegment(v[4], v[1]),
               LineSegment(v[5], v[6]), # upper z
               LineSegment(v[6], v[7]),
               LineSegment(v[7], v[8]),
               LineSegment(v[8], v[5]),
               LineSegment(v[1], v[5]), # lower, upper connections
               LineSegment(v[2], v[6]),
               LineSegment(v[3], v[7]),
               LineSegment(v[4], v[8]))
end

function faces(aab::AABox{3})
    v = vertices(aab)
    return Vec(Quadrilateral(v[1], v[2], v[3], v[4]),
               Quadrilateral(v[5], v[6], v[7], v[8]),
               Quadrilateral(v[1], v[2], v[6], v[5]),
               Quadrilateral(v[2], v[3], v[7], v[6]),
               Quadrilateral(v[3], v[4], v[8], v[7]),
               Quadrilateral(v[4], v[1], v[5], v[8]))
end

function Base.show(io::IO, aab::AABox)
    return print(io, "AABox($(minima(aab)), $(maxima(aab)))")
end
