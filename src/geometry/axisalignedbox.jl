export AABox
export measure, xmin, ymin, zmin, xmax, ymax, zmax, Œîx, Œîy, Œîz,
       facets, ridges, peaks, faces, edges, vertices

"""
    AABox(minima::Point{Dim,T}, maxima::Point{Dim,T})

A `Dim`-dimensional axis-aligned bounding box using two `Dim`-dimensional
points, representing the `minima` and `maxima` of the box. 
"""
struct AABox{Dim,T}
    minima::Point{Dim,T}
    maxima::Point{Dim,T}
    function AABox{Dim,T}(minima::Point{Dim,T}, maxima::Point{Dim,T}) where {Dim,T}
        for i ‚àà 1:Dim
            if maxima[i] ‚â§ minima[i]
                error("AABox maxima must be greater that the minima")
            end
        end
        return new{Dim,T}(minima, maxima)
    end
end

# constructors
function AABox(minima::Point{Dim,T}, maxima::Point{Dim,T}) where {Dim,T}
    return AABox{Dim,T}(minima, maxima)
end
AABox(minima, maxima) = AABox(Point(minima), Point(maxima))

Base.isapprox(aab‚ÇÅ::AABox, aab‚ÇÇ::AABox) = aab‚ÇÅ.minima ‚âà aab‚ÇÇ.minima && 
                                          aab‚ÇÅ.maxima ‚âà aab‚ÇÇ.maxima 
xmin(aab::AABox) = aab.minima[1] 
ymin(aab::AABox) = aab.minima[2] 
zmin(aab::AABox) = aab.minima[3] 
xmax(aab::AABox) = aab.maxima[1] 
ymax(aab::AABox) = aab.maxima[2] 
zmax(aab::AABox) = aab.maxima[3] 
Œîx(aab::AABox) = xmax(aab) - xmin(aab) 
Œîy(aab::AABox) = ymax(aab) - ymin(aab)
Œîz(aab::AABox) = zmax(aab) - zmin(aab)

measure(aab::AABox) = prod(aab.maxima - aab.minima) 

function ridges(aab::AABox{2})
    # Ordered CCW
    return Vec(Point(xmin(aab), ymin(aab)),
               Point(xmax(aab), ymin(aab)),
               Point(xmax(aab), ymax(aab)),
               Point(xmin(aab), ymax(aab))
              )
end
function facets(aab::AABox{2})
    v = ridges(aab)
    return Vec(LineSegment(v[1], v[2]),
               LineSegment(v[2], v[3]),
               LineSegment(v[3], v[4]),
               LineSegment(v[4], v[1])
              )
end

edges(aab::AABox{2}) = facets(aab)
vertices(aab::AABox{2}) = ridges(aab)

function peaks(aab::AABox{3})
    # in CCW order, low z then high z
    #      y
    #      ^
    #      |
    #      |
    #      |------> x
    #     /   
    #    /   
    #   ùò∑
    #  z
    #
    #   4----3
    #  /    /|
    # 8----7 |
    # |    | 2
    # |    |/
    # 5----6
    return Vec(Point(xmin(aab), ymin(aab), zmin(aab)),
               Point(xmax(aab), ymin(aab), zmin(aab)),
               Point(xmax(aab), ymax(aab), zmin(aab)),
               Point(xmin(aab), ymax(aab), zmin(aab)),
               Point(xmin(aab), ymin(aab), zmax(aab)),             
               Point(xmax(aab), ymin(aab), zmax(aab)),
               Point(xmax(aab), ymax(aab), zmax(aab)),
               Point(xmin(aab), ymax(aab), zmax(aab))
              )
end

function ridges(aab::AABox{3})
    # in CCW order, low z, then high z, then the segments that attach low and
    # high z in CCW order.
    #      y
    #      ^
    #      |
    #      |
    #      |------> x
    #     /   
    #    /   
    #   ùò∑
    #  z
    #       3
    #    +----+
    # 11/ 7  /|
    #  +----+ | 2
    # 8|    |6+
    #  |    |/ 9
    #  +----+
    #     5
    v = peaks(aab)
    return Vec(LineSegment(v[1], v[2]), # lower z
               LineSegment(v[2], v[3]),
               LineSegment(v[3], v[4]),
               LineSegment(v[4], v[1]),
               LineSegment(v[5], v[6]), # upper z
               LineSegment(v[6], v[7]),
               LineSegment(v[7], v[8]),
               LineSegment(v[8], v[5]),
               LineSegment(v[1], v[5]), # lower, upper connections
               LineSegment(v[2], v[6]),
               LineSegment(v[3], v[7]),
               LineSegment(v[4], v[8])
              )
end

function facets(aab::AABox{3})
     v = peaks(aab)   
     return Vec(Quadrilateral(v[1], v[2], v[3], v[4]),
                Quadrilateral(v[5], v[6], v[7], v[8]),
                Quadrilateral(v[1], v[2], v[6], v[5]),
                Quadrilateral(v[2], v[3], v[7], v[6]),
                Quadrilateral(v[3], v[4], v[8], v[7]),
                Quadrilateral(v[4], v[1], v[5], v[8]),
               )
end

faces(aab::AABox{3}) = facets(aab)
edges(aab::AABox{3}) = ridges(aab)
vertices(aab::AABox{3}) = peaks(aab)

function Base.show(io::IO, aab::AABox)
    print(io, "AABox($(aab.minima), $(aab.maxima))")
end
